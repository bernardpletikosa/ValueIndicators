/*
 * Copyright 2013 Chris Banes
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven'
apply plugin: 'signing'

def isReleaseBuild() {
    return android.defaultConfig.versionName.contains("SNAPSHOT") == false
}

def getRepositoryUsername() {
    return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
    return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.groupId = "com.github.bernardpletikosa"
                pom.artifactId = "value-indicators"
                pom.version = android.defaultConfig.versionName

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    //read from ~/.gradle/gradle.properties
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    //read from ~/.gradle/gradle.properties
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                pom.project {
                    name "Android Value Indicators"
                    packaging "jar"
                    description "Android Library for graphic value representation."
                    url "https://github.com/bernardpletikosa/value-indicators"

                    scm {
                        url 'scm:git@github.com:bernardpletikosa/value-indicators.git'
                        connection 'scm:git@github.bernardpletikosa/value-indicators.git'
                        developerConnection 'scm:git@github.bernardpletikosa/value-indicators.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License'
                            url 'https://github.com/bernardpletikosa/value-indicators/blob/master/LICENSE'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'bernardpletikosa'
                            name 'Bernard Pletikosa'
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task androidJavadocsJar(type: Jar, dependsOn: bundleJavadocRelease) {
        classifier = 'javadoc'
        from bundleJavadocRelease.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.sourceFiles
    }

    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}
